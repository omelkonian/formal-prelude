{-# OPTIONS --allow-exec --with-K #-}
{-# OPTIONS -v modelcheck:100 #-}
open import Agda.Builtin.Reflection.External using (execTC)

open import Prelude.Init
open Integer hiding (show; _>_; _â‰¥_; _<_; _â‰¤_)
open Meta
open import Prelude.Generics
open Debug ("modelcheck" , 100)
open import Prelude.Ord
open import Prelude.Semigroup
open import Prelude.Show
open import Prelude.Monad
open import Prelude.DecEq
open import Prelude.FromList; open import Prelude.ToList
open import Prelude.Tactics.PostulateIt
open import Prelude.Nary

private variable A B S : Set

-- ** state machine definition
record StateMachine (S : Set) : Set where
  field init : S
        step : S â†’ S
open StateMachine

Invariant : StateMachine S â†’ Predâ‚€ S â†’ Set
Invariant sm P = P (sm .init) Ã— (âˆ€ s â†’ P s â†’ P (sm .step s))

-- ** translation to Kind2 syntax
record ToCode (A : Set) : Set where
  field toCode : A â†’ String
open ToCode â¦ƒ...â¦„ public

pattern `pos l = con (quote â„¤.pos) (vArg l âˆ· [])
pattern `if_then_else_ b x y =
  def (quote if_then_else_) (hArg _ âˆ· hArg _ âˆ· vArg b âˆ· vArg x âˆ· vArg y âˆ· [])

toCode-deBruijn : â„• â†’ String
toCode-deBruijn = Î» where
  0 â†’ "ðŸ˜"; 1 â†’ "ðŸ™"; 2 â†’ "ðŸš"; 3 â†’ "ðŸ›"; 4 â†’ "ðŸœ"
  5 â†’ "ðŸ"; 6 â†’ "ðŸž"; 7 â†’ "ðŸŸ"; 8 â†’ "ðŸ "; 9 â†’ "ðŸ¡"
  _ â†’ "$"

toCode-binOp toCode-unOp toCode-nullOp : Name â†’ String
toCode-nullOp n = case show n of Î» where
  "Int" â†’ "int"; "â„¤" â†’ "int"
  "0â„¤"  â†’ "0"; "1â„¤"  â†’ "1"
  s â†’ s
toCode-unOp n = case show n of Î» where
  "Â¬_" â†’ "not"
  s â†’ s
toCode-binOp n = case show n of Î» where
  "_>áµ‡_" â†’ ">"; "_â‰¥áµ‡_" â†’ ">="; "_<áµ‡_" â†’ "<"; "_â‰¤áµ‡_" â†’ "<="
  "_>_" â†’ ">"; "_â‰¥_" â†’ ">="; "_<_" â†’ "<"; "_â‰¤_" â†’ "<="
  "_-_" â†’ "-"
  s â†’ s

instance
  ToCode-â„• : ToCode â„•
  ToCode-â„• .toCode = show

  ToCode-â„¤ : ToCode â„¤
  ToCode-â„¤ .toCode = Î» where
    (Integer.+_ n)     â†’ toCode n
    (Integer.-[1+_] n) â†’ "-" â—‡ toCode n

  ToCode-Float : ToCode Float
  ToCode-Float .toCode = show

  ToCode-Char : ToCode Char
  ToCode-Char .toCode = show

  ToCode-String : ToCode String
  ToCode-String .toCode s = "\"" â—‡ show s â—‡ "\""

  ToCode-Literal : ToCode Literal
  ToCode-Literal .toCode = Î» where
    (nat    n) â†’ toCode n
    (float  f) â†’ toCode f
    (char   c) â†’ toCode c
    (string s) â†’ toCode s
    l â†’ show l

  {-# TERMINATING #-}
  ToCode-Term : ToCode Term
  ToCode-Term .toCode = Î» where
    (lit l) â†’ toCode l
    (var n []) â†’ toCode-deBruijn n
    (`pos l) â†’ toCode l
    (`if b then x else y) â†’
      "if " â—‡ toCode b â—‡ " then " â—‡ toCode x â—‡ " else " â—‡ toCode y
    t@(def op as) â†’
      case vArgs as of Î» where
        [] â†’ toCode-nullOp op
        (x âˆ· []) â†’ toCode-unOp op â—‡ " (" â—‡ toCode x â—‡ ")"
        (x âˆ· y âˆ· []) â†’ toCode x â—‡ " " â—‡ toCode-binOp op â—‡ " " â—‡ toCode y
        (_ âˆ· x âˆ· y âˆ· []) â†’ toCode x â—‡ " " â—‡ toCode-binOp op â—‡ " " â—‡ toCode y
        _ â†’ show t
    t â†’ show t

-- ** counter state machine
State = â„¤

Counter : StateMachine State
Counter = Î» where
  .init â†’ + 42
  .step i â†’ if i >áµ‡ 0â„¤ then i - 1â„¤ else 0â„¤
-- Counter = record
--   { init = + 42
--   ; step = Î» i â†’ if i >áµ‡ 0â„¤ then i - 1â„¤ else 0â„¤
--   }

NoNegatives : Predâ‚€ State
NoNegatives = Î» i â†’ Â¬ (i < + 0)

-- ** reflection
private postulate
  solvedByKind2 : âˆ€ {A : Set â„“} â†’ A
macro
  solveWithKind2 : Hole â†’ TC âŠ¤
  solveWithKind2 hole = do
    (def (quote Invariant) as) â† inferType hole
      where _ â†’ error "goal type is not an `Invariant`"
    (n âˆ™ âˆ· p@(def pn _) âˆ· []) â† return $ vArgs as
      where _ â†’ error "hole should be of type `Invariant _ _`"
    (lam visible (abs _ p)) â† reduce p
      where _ â†’ error "invariant property should be of the form `Î» x â†’ P x`"
    (def (quote StateMachine) (vArg ty âˆ· [])) â† getType n
      where _ â†’ error "not of type `StateMachine _`"
    ty â† reduce ty
    function (clause _ _ body âˆ· []) â† getDefinition n
      where _ â†’ error $ show n â—‡ " is not defining a record value"
    (i , x , t) â† getRecordValue body
    let s = "node " â—‡ show n â—‡ "() returns (" â—‡ x â—‡ " : " â—‡ toCode ty â—‡ ");\n"
          â—‡ "let\n"
          â—‡ "  " â—‡ x â—‡ " = " â—‡ toCode i â—‡ " -> \n"
          â—‡ "    " â—‡ replace ('ðŸ˜' , "(pre " â—‡ x â—‡ ")") (toCode t) â—‡ ";\n"
          â—‡ "  check \"" â—‡ show pn â—‡ "\" " â—‡ replace ('ðŸ˜' , x) (toCode p) â—‡ ";\n"
          â—‡ "tel\n"
    print s
    (errCode , stdout , _) â† execTC "kind2" âŸ¦ "--color" , "false" âŸ§ s
    print $ "errCode: " â—‡ show errCode
    case errCode of Î» where
      20 â†’ print stdout >> unify hole (quote solvedByKind2 âˆ™)
      _  â†’ error stdout
    where
      replace : Char Ã— String â†’ String â†’ String
      replace (câ‚€ , x) = fromList
                       âˆ˜ concatMap (Î» c â†’ if c == câ‚€ then toList x else [ c ])
                       âˆ˜ toList

      getRecordValue : Term â†’ TC (Term Ã— String Ã— Term)
      getRecordValue t with t
      ... | con _ (_ âˆ· vArg i âˆ· vArg (lam visible (abs x t)) âˆ· [])
          = return (i , x , t)
      ... | pat-lam (clause _ _ i âˆ· clause ((x , _) âˆ· []) _ t âˆ· []) _
          = return (i , x , t)
      ... | _ = error $ "not a record value: " â—‡ show t

_ : Invariant Counter NoNegatives
_ = solveWithKind2
